"""
A Snakefile is just a python file! You can do things like:
* import functions,
* create variables,
* perform calculations,
* write comments.

URL: https://raw.githubusercontent.com/samgdotson/2021-dotson-ms/refs/heads/master/data/uiuc_demand_data.csv
"""
configfile: "config.yml"

data_path = "data"
figure_path = "figures"

rule targets:
    input:
        dg_vs_central = f"{figure_path}/dg_vs_central.png",
        dg_by_type = f"{figure_path}/dg_by_type.png",
        pv_vs_elec_cost = f"{figure_path}/pv_vs_elec_cost.png",
        dag = "dag.png"

rule retrieve_data:
    output:
        dg_raw = f"{data_path}/dg_raw.csv", 
        nm_raw = f"{data_path}/nm_raw.csv", 
        nnm_raw = f"{data_path}/nnm_raw.csv", 
        central_raw = f"{data_path}/central_raw.csv",
        res_costs_raw = f"{data_path}/res_costs_raw.csv",
    script: "scripts/retrieve_data.py"

rule process_data:
    input: 
        dg_raw = f"{data_path}/dg_raw.csv", 
        nm_raw = f"{data_path}/nm_raw.csv", 
        nnm_raw = f"{data_path}/nnm_raw.csv", 
        central_raw = f"{data_path}/central_raw.csv",
        res_costs_raw = f"{data_path}/res_costs_raw.csv",
        pv_costs_raw = f"{data_path}/National_Residential_Installed Prices (2000-2023).csv"
    output:
        combined_data = f"{data_path}/combined_data.csv",
        distributed_tech_data = f"{data_path}/distributed_tech_data.csv",
        costs_data = f"{data_path}/costs_data.csv",
    script: "scripts/process_data.py" 

rule plot_data:
    input:
        combined_data = f"{data_path}/combined_data.csv",
        distributed_tech_data = f"{data_path}/distributed_tech_data.csv",
        costs_data = f"{data_path}/costs_data.csv",
    output: 
        dg_vs_central = f"{figure_path}/dg_vs_central.png",
        dg_by_type = f"{figure_path}/dg_by_type.png",
        pv_vs_elec_cost = f"{figure_path}/pv_vs_elec_cost.png",
    script: "scripts/plot_data.py"

rule build_dag:
    input: "Snakefile"
    output:
        "dag.png"
    shell:
        "snakemake --dag | dot -Tpng > {output}"